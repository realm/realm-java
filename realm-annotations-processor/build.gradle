buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

version = new File("${projectDir}/../version.txt").text
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
    jcenter()
}

Properties localProperties = new Properties()
localProperties.load(new FileInputStream("${projectDir}/../local.properties"))
localProperties.entrySet().each() { entry ->
    project.ext[entry.getKey()] = localProperties.setProperty(entry.getKey(), entry.getValue())
}

dependencies {
    compile group:'com.squareup', name:'javawriter', version:'2.5.0'
    compile files("../realm-annotations/build/libs/realm-annotations-${version}.jar")
    testCompile files('../realm/build/intermediates/bundles/debug/classes.jar')
    testCompile files("${System.properties['java.home']}/../lib/tools.jar") // This is needed otherwise compile-testing won't be able to find it
    testCompile group:'junit', name:'junit', version:'4.11'
    testCompile group:'com.google.testing.compile', name:'compile-testing', version:'0.6'
    testCompile files(new File(project.ext["sdk.dir"], '/platforms/android-21/android.jar'))
}

jar {
    from configurations.compile.findAll {it.name.contains('javawriter')}.collect { zipTree(it) }
}

shadowJar {
    relocate 'com.squareup.javawriter', 'io.realm.processor.javawriter'
}

//for Ant filter
import org.apache.tools.ant.filters.ReplaceTokens

task generateVersionClass(type: Copy) {
    from 'src/main/templates/Version.java'
    into 'build/generated-src/main/java/io/realm/processor'
    filter(ReplaceTokens, tokens: [version: version])
    outputs.upToDateWhen { false }
}

compileJava.dependsOn generateVersionClass

sourceSets {
    main {
        java {
            srcDir 'build/generated-src/main/java'
        }
    }
}

task compileAnnotations(type: GradleBuild) {
    dir = file('../realm-annotations')
    tasks = ['assemble']
}

task assembleRealm(type: GradleBuild) {
    dir = file('../realm')
    tasks = ['assemble']
}

tasks.assemble {
    dependsOn compileAnnotations
}

tasks.check {
    dependsOn assembleRealm
}
