buildscript {
    def properties = new Properties()
    properties.load(new FileInputStream("${projectDir}/../dependencies.list"))
    ext.kotlin_version = properties.get('KOTLIN')
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:${properties.get("GRADLE_NEXUS_PLUGIN")}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'io.realm'
version = file("${projectDir}/../version.txt").text.trim()

def properties = new Properties()
properties.load(new FileInputStream("${projectDir}/../dependencies.list"))

def coreVersion = properties.getProperty('REALM_CORE')

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenLocal()
    google()
    jcenter()
}

configurations {
    provided
    compile.extendsFrom provided
}

// Include the generated Version file
sourceSets {
    main {
        compileClasspath += configurations.provided
        java.srcDirs += ['build/generated-src/main/java', 'src/main/kotlin']
    }
}

dependencies {
    implementation gradleApi()
    implementation "io.realm:realm-annotations:${version}"
    compileOnly "com.android.tools.build:gradle:${properties.get("GRADLE_BUILD_TOOLS")}"
    implementation 'org.javassist:javassist:3.25.0-GA'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    implementation "com.google.guava:guava:31.0.1-jre"
    // JAX-B dependencies for JDK 9+ (this is not available in JVM env 'java.lang.NoClassDefFoundError: javax/xml/bind/DatatypeConverter'
    // and it was removed in Java 11 https://stackoverflow.com/a/43574427
    implementation("javax.xml.bind:jaxb-api:2.3.1")

    testImplementation 'junit:junit:4.12'
}

// for Ant filter
import org.apache.tools.ant.filters.ReplaceTokens

task generateVersionClass(type: Copy) {
    from 'src/main/templates/Version.java'
    into 'build/generated-src/main/java/io/realm/transformer'
    filter(ReplaceTokens, tokens: [version: version, coreVersion: coreVersion])
    outputs.upToDateWhen { false }
}

compileKotlin.dependsOn generateVersionClass

apply from: "${rootDir}/../mavencentral-publications.gradle"
apply from: "${rootDir}/../mavencentral-publish.gradle"

publishing {
    publications {
        realmPublication(MavenPublication) {
            groupId 'io.realm'
            artifactId = 'realm-transformer'
            from components.java
            populatePom(
                    it,
                    'realm-transformer',
                    'Android Gradle Transformer for Realm. Realm is a mobile database: Build better apps, faster.'
            )
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xinline-classes", "-Xjvm-default=enable"]
    }
}
