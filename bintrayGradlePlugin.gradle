buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def currentVersion = file("${projectDir}/version.txt").text.trim();

version = currentVersion

def commonPom = {
    version = currentVersion
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    issueManagement {
        system 'github'
        url 'https://github.com/realm/realm-java/issues'
    }
    scm {
        url 'scm:https://github.com/realm/realm-java'
        connection 'scm:git@github.com:realm/realm-java.git'
        developerConnection 'scm:git@github.com:realm/realm-java.git'
    }
}

publishing {
    publications {
        gradlePlugin(MavenPublication) {
            groupId 'io.realm'
            artifactId = 'realm-gradle-plugin'
            artifact file("${rootDir}/gradle-plugin/build/libs/realm-gradle-plugin-${currentVersion}.jar")
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'realm-gradle-plugin')
                root.appendNode('description', 'Gradle plugin for Realm. Realm is a mobile database: a replacement for SQLite & ORMs.')
                root.appendNode('url', 'http://realm.io')
                def dependenciesNode = root.appendNode('dependencies')
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'com.neenbedankt.gradle.plugins')
                dependencyNode.appendNode('artifactId', 'android-apt')
                dependencyNode.appendNode('version', '1.8')
                root.children().last() + commonPom
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : 'noUser'
    key = project.hasProperty('bintrayKey') ? bintrayKey : 'noKey'

    dryRun = false
    publish = false

    publications = ['gradlePlugin']
    pkg {
        repo = 'maven'
        name = 'realm-gradle-plugin'
        desc = 'Realm for Android'
        websiteUrl = 'http://realm.io'
        issueTrackerUrl = 'https://github.com/realm/realm-java/issues'
        vcsUrl = 'https://github.com/realm/realm-java.git'
        licenses = ['Apache-2.0']
        labels = ['android', 'realm']
        publicDownloadNumbers = false
    }
}