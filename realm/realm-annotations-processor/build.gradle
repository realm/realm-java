apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

Properties localProperties = new Properties()
localProperties.load(new FileInputStream("${rootDir}/local.properties"))
localProperties.entrySet().each() { entry ->
    project.ext[entry.getKey()] = localProperties.setProperty(entry.getKey(), entry.getValue())
}

dependencies {
    compile group:'com.squareup', name:'javawriter', version:'2.5.0'
    compile project(':realm-annotations')
    testCompile files('../realm-library/build/intermediates/bundles/release/classes.jar') // Java projects cannot depend on AAR files
    testCompile files("${System.properties['java.home']}/../lib/tools.jar") // This is needed otherwise compile-testing won't be able to find it
    testCompile group:'junit', name:'junit', version:'4.11'
    testCompile group:'com.google.testing.compile', name:'compile-testing', version:'0.6'
    testCompile files(new File(project.ext["sdk.dir"], '/platforms/android-21/android.jar'))
}

//for Ant filter
import org.apache.tools.ant.filters.ReplaceTokens

task generateVersionClass(type: Copy) {
    from 'src/main/templates/Version.java'
    into 'build/generated-src/main/java/io/realm/processor'
    filter(ReplaceTokens, tokens: [version: version])
    outputs.upToDateWhen { false }
}

// Include the generated Version file
sourceSets {
    main {
        java {
            srcDir 'build/generated-src/main/java'
        }
    }
}

compileJava.dependsOn generateVersionClass
compileTestJava.dependsOn ':realm-library:assemble'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://localhost/Users/emanuelez/.android-sdk/extras/android/m2repository")
        }
    }
}

// Generates the Jar for Eclipse
shadowJar {
    from zipTree('../realm-library/build/intermediates/bundles/release/classes.jar')
    relocate 'com.squareup.javawriter', 'io.realm.processor.javawriter'
    baseName = 'realm-android'
    classifier = ''
}

shadowJar.dependsOn ':realm-library:assemble'
