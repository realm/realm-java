import java.security.MessageDigest

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'com.github.spotbugs-base'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'de.undercouch.download'
apply plugin: 'net.ltgt.errorprone'

def properties = new Properties()
properties.load(new FileInputStream("${projectDir}/../../dependencies.list"))

ext.ccachePath = project.findProperty('ccachePath') ?: System.getenv('NDK_CCACHE')
ext.lcachePath = project.findProperty('lcachePath') ?: System.getenv('NDK_LCACHE')

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.compileSdkVersion
        versionName version
        project.archivesBaseName = "realm-android-library"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        externalNativeBuild {
            cmake {
                if (project.ccachePath) arguments "-DNDK_CCACHE=$project.ccachePath"
                if (project.lcachePath) arguments "-DNDK_LCACHE=$project.lcachePath"
                if (project.hasProperty('buildTargetABIs') && !project.getProperty('buildTargetABIs').trim().isEmpty()) {
                    abiFilters(*project.getProperty('buildTargetABIs').trim().split('\\s*,\\s*'))
                } else {
                    // "armeabi" and "mips" are no longer supported by the NDK
                    abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
                }

                arguments "-DREALM_JAVA_BUILD_CORE_FROM_SOURCE=${project.hasProperty('buildCore') && project.getProperty('buildCore').toBoolean()}"
                arguments "-DCMAKE_INTERPROCEDURAL_OPTIMIZATION=${!project.hasProperty('enableLTO') || project.getProperty('enableLTO').toBoolean()}"
                targets "realm-jni"
            }
        }

        buildTypes {
            debug {
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments += ['realm.suppressWarnings': 'false']
                    }
                }
            }
        }
    }

    ndkVersion = project.findProperty('ndkVersion')

    externalNativeBuild {
        cmake {
            version = properties.getProperty('CMAKE')
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    buildTypes {
        debug {
            // FIXME: If enabled, crashes with https://issuetracker.google.com/issues/37116868
            testCoverageEnabled = false
            // minifyEnabled = true;
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            externalNativeBuild {
                cmake {
                }
            }
            // minifyEnabled = true;
        }
    }

    sourceSets {
        testObjectServer {
            java.srcDirs += ['src/testObjectServer/kotlin']
        }
        androidTest {
            java.srcDirs += ['src/androidTest/kotlin', 'src/testUtils/java', 'src/testUtils/kotlin']
        }
        androidTestObjectServer {
            java.srcDirs += [/* FIXME 'src/syncIntegrationTest/java', */
                             'src/syncIntegrationTest/kotlin',
                             'src/androidTestObjectServer/kotlin', 'src/syncTestUtils/java'
                             , 'src/syncTestUtils/kotlin']
            assets.srcDirs += ['src/syncIntegrationTest/assets/']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        // We did strip with cmake for release build.
        // Please, Gradle, you are not that smart! Pleas DO NOT strip debug symbols for debug build!
        doNotStrip "*/*/*.so"
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions 'api'

    productFlavors {
        base {
            dimension 'api'
            externalNativeBuild {
                cmake {
                    arguments "-DREALM_FLAVOR=base"
                }
            }
            consumerProguardFiles 'proguard-rules-consumer-common.pro', 'proguard-rules-consumer-base.pro'
            proguardFiles 'proguard-rules-build-common.pro'
        }
        objectServer {
            dimension 'api'
            externalNativeBuild {
                cmake {
                    arguments "-DREALM_FLAVOR=objectServer"
                }
            }
            consumerProguardFiles 'proguard-rules-consumer-common.pro', 'proguard-rules-consumer-objectServer.pro'
            proguardFiles 'proguard-rules-build-common.pro', 'proguard-rules-build-objectServer.pro'
        }
    }
}


project.afterEvaluate {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Werror'
    }

    tasks.all { task ->
        android.productFlavors.all { flavor ->
            if (task.name == "publish${flavor.name.capitalize()}PublicationPublicationToMavenLocal") {
                task.dependsOn "assemble${flavor.name.capitalize()}"
            }
        }
    }
}

// enable @ParametersAreNonnullByDefault annotation. See https://blog.jetbrains.com/kotlin/2017/09/kotlin-1-1-50-is-out/
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
    }
}

coveralls.jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"

android.registerTransform(new io.realm.transformer.RealmTransformer(project))
android.registerTransform(new io.realm.buildtransformer.RealmBuildTransformer(
        "base",
        "io.realm.internal.annotations.ObjectServer",
        [
            "io_realm_sync_permissions_ClassPermissionsRealmProxyInterface.class",
            "io_realm_sync_permissions_PermissionRealmProxyInterface.class",
            "io_realm_sync_permissions_PermissionUserRealmProxyInterface.class",
            "io_realm_sync_permissions_RealmPermissionsRealmProxyInterface.class",
            "io_realm_sync_permissions_RoleRealmProxyInterface.class"
        ].toSet()
))

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    compileOnly "io.reactivex.rxjava2:rxjava:${properties.getProperty('RXJAVA_DEPENDENCY')}"
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.1.2'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_android_version"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_android_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_android_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_android_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    api "io.realm:realm-annotations:${version}"
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.getkeepsafe.relinker:relinker:1.4.0'
    api "org.mongodb:bson:${properties.getProperty('BSON_DEPENDENCY')}"
    implementation("io.reactivex.rxjava2:rxandroid:${properties.getProperty('RXANDROID_DEPENDENCY')}") {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
    }

    androidTestImplementation "androidx.multidex:multidex:2.0.1"

    kapt project(':realm-annotations-processor') // See https://github.com/realm/realm-java/issues/5799
    objectServerImplementation 'com.squareup.okhttp3:okhttp:3.12.0' // Going above this requires minSDK 21
    kaptAndroidTest project(':realm-annotations-processor')
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    androidTestImplementation "io.reactivex.rxjava2:rxjava:${properties.getProperty('RXJAVA_DEPENDENCY')}"
    androidTestImplementation "io.reactivex.rxjava2:rxandroid:${properties.getProperty('RXANDROID_DEPENDENCY')}"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    androidTestImplementation "org.skyscreamer:jsonassert:1.5.0"
    androidTestImplementation project(':kotlin-extensions')

    // specify error prone version to prevent sudden failure
    errorprone 'com.google.errorprone:error_prone_core:2.1.2'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.objectServer.java.srcDirs
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

def betaTag = 'Beta:a:<div style="border-style:solid;border-width:2px">This software is considered in beta phase. ' +
        'It indicates that any public interface can change without prior announcements. ' +
        'Moreover, classes, constructors, and methods annotated as beta are not ' +
        'considered at production quality, and should be used with care.</div>'

task javadoc(type: Javadoc) {
    source android.sourceSets.objectServer.java.srcDirs
    source android.sourceSets.main.java.srcDirs
    source "../../realm-annotations/src/main/java"
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        title = "Realm ${project.version}"
        memberLevel = JavadocMemberLevel.PUBLIC
        docEncoding = 'UTF-8'
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        locale = 'en_US'
        overview = 'src/overview.html'

        links "https://docs.oracle.com/javase/7/docs/api/"
        links "http://reactivex.io/RxJava/javadoc/"
        // TODO We probably need to add the bson.jar to the classpath for these to work
        links "https://www.javadoc.io/doc/org.mongodb/bson/${properties.getProperty('BSON_DEPENDENCY')}/"
        linksOffline "https://developer.android.com/reference/", "${project.android.sdkDirectory}/docs/reference"

        tags = [betaTag]
    }
    exclude '**/internal/**'
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/*.kt'
    doLast {
        copy {
            from "src/realm-java-overview.png"
            into "$buildDir/docs/javadoc"
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// See https://spotbugs-gradle-plugin.netlify.app/com/github/spotbugs/snom/spotbugsextension
spotbugs {
    toolVersion = '4.1.1'
    ignoreFailures = false
    effort = "default"
    reportLevel = "medium"
    excludeFilter = file("${projectDir}/../config/findbugs/findbugs-filter.xml")
}

// See https://spotbugs.readthedocs.io/en/latest/migration.html#findbugs-gradle-plugin
task spotbugsMain(type: com.github.spotbugs.snom.SpotBugsTask) {
    dependsOn 'assemble'
    group = 'verification'
    classes = fileTree("build/intermediates/javac/")

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task pmd(type: Pmd) {
    group = 'verification'

    source = fileTree('src/main/java') + fileTree('src/objectServer/java')
    ruleSetFiles = files("${projectDir}/../config/pmd/ruleset.xml")

    ruleSets = []

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// Configure Checkstyle
// Android sourceSets are not sourceSets, so we can't confgure this with the DSL.
task checkstyle(type: Checkstyle) {
    group = 'verification'
    configFile file("${projectDir}/../config/checkstyle/checkstyle.xml")

    source fileTree('src/main/java') + fileTree('src/objectServer/java')
    include '**/*.java'
    exclude '**/gen/**'

    // empty classpath
    classpath = files()

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'realm-android-library'
                description 'Realm is a mobile database: a replacement for SQLite & ORMs.'
                url 'https://docs.mongodb.com/realm/'

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                issueManagement {
                    system 'github'
                    url 'https://github.com/realm/realm-java/issues'
                }
                scm {
                    url 'scm:https://github.com/realm/realm-java'
                    connection 'scm:git@github.com:realm/realm-java.git'
                    developerConnection 'scm:git@github.com:realm/realm-java.git'
                }
            }
        }
    }
}

// The publications doesn't know about our AAR dependencies, so we have to manually add them to the pom
// Credit: http://stackoverflow.com/questions/24743562/gradle-not-including-dependencies-in-published-pom-xml
def createPomDependencies(configurationNames) {
    return {
        def dependenciesNode = asNode().appendNode('dependencies')
        configurationNames.each { configurationName ->
            configurations[configurationName].allDependencies.each {
                if (it.group != null && it.name != null) {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)

                    //If there are any exclusions in dependency
                    if (it.excludeRules.size() > 0) {
                        def exclusionsNode = dependencyNode.appendNode('exclusions')
                        it.excludeRules.each { rule ->
                            def exclusionNode = exclusionsNode.appendNode('exclusion')
                            exclusionNode.appendNode('groupId', rule.group)
                            exclusionNode.appendNode('artifactId', rule.module)
                        }
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        basePublication(MavenPublication) {
            groupId 'io.realm'
            artifactId 'realm-android-library'
            version project.version
            artifact file("${rootDir}/realm-library/build/outputs/aar/realm-android-library-base-release.aar")
            artifact sourcesJar
            artifact javadocJar

            pom.withXml(createPomDependencies(["baseImplementation", "implementation", "baseApi", "api"]))
        }

        objectServerPublication(MavenPublication) {
            groupId 'io.realm'
            artifactId 'realm-android-library-object-server'
            version project.version
            artifact file("${rootDir}/realm-library/build/outputs/aar/realm-android-library-objectServer-release.aar")
            artifact sourcesJar
            artifact javadocJar

            pom.withXml(createPomDependencies(["objectServerImplementation", "implementation", "objectServerApi", "api"]))
        }
    }
    repositories {
        maven {
            credentials(AwsCredentials) {
                accessKey project.hasProperty('s3AccessKey') ? s3AccessKey : 'noAccessKey'
                secretKey project.hasProperty('s3SecretKey') ? s3SecretKey : 'noSecretKey'
            }
            if (project.version.endsWith('-SNAPSHOT')) {
                url "s3://realm-ci-artifacts/maven/snapshots/"
            } else {
                url "s3://realm-ci-artifacts/maven/releases/"
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = project.hasProperty('bintrayUser') ? bintrayUser : 'noUser'
            password = project.hasProperty('bintrayKey') ? bintrayKey : 'noKey'
        }
        defaults {
            publications('basePublication', 'objectServerPublication')
            publishPom = true
            publishIvy = false
        }
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishToMavenLocal.dependsOn assemble

if (project.hasProperty('dontCleanJniFiles')) {
    project.afterEvaluate {
        tasks.all { task ->
            if (task.name.startsWith('externalNativeBuildClean')) {
                task.enabled = false
            }
        }
    }
} else {
    task cleanExternalBuildFiles(type: Delete) {
        delete project.file('.externalNativeBuild')
        delete project.file('.cxx')
        // Clean .so files that were created by old build script (realm/realm-jni/build.gradle).
        delete project.file('src/main/jniLibs')
    }
    clean.dependsOn cleanExternalBuildFiles
}

project.afterEvaluate {
    android.libraryVariants.all { variant ->
        if (project.hasProperty('buildTargetABIs') && project.getProperty('buildTargetABIs').trim().isEmpty()) {
            variant.externalNativeBuildProviders[0].configure {
                enabled = false
            }
        }

        // all Java files must be compiled before native build
        // See https://github.com/android/ndk-samples/issues/284
        android.libraryVariants.all { anotherVariant ->
            if (variant.flavorName == anotherVariant.flavorName) {
                variant.externalNativeBuildProviders[0].configure {
                    dependsOn "compile${anotherVariant.name.capitalize()}JavaWithJavac"
                }
            }
        }
        // as of android gradle plugin 3.0.0-alpha5, generateJsonModel* triggers native build. Java files must be compiled before them.
        android.buildTypes.all { buildType ->
            tasks["generateJsonModel${variant.name.capitalize()}"].dependsOn "compile${variant.flavorName.capitalize()}${buildType.name.capitalize()}JavaWithJavac"
        }
    }
}

android.productFlavors.all { flavor ->
    def librarySuffix = flavor.name == 'base' ? '' : '-object-server'
    def userName = project.findProperty('bintrayUser') ?: 'noUser'
    def accessKey = project.findProperty('bintrayKey') ?: 'noKey'
    def artifactId = "realm-android-library${librarySuffix}"
    // BINTRAY

    task("bintrayAar${flavor.name.capitalize()}", type: Exec) {
        dependsOn "assemble${flavor.name.capitalize()}"
        group = 'Publishing'
        commandLine 'curl',
                '-X',
                'PUT',
                '-T',
                "${buildDir}/outputs/aar/realm-android-library-${flavor.name}-release.aar",
                '-u',
                "${userName}:${accessKey}",
                "https://api.bintray.com/content/realm/maven/${artifactId}/${project.version}/io/realm/${artifactId}/${project.version}/${artifactId}-${project.version}.aar?publish=0"
    }

    task("bintraySources${flavor.name.capitalize()}", type: Exec) {
        dependsOn sourcesJar
        group = 'Publishing'
        commandLine 'curl',
                '-X',
                'PUT',
                '-T',
                "${buildDir}/libs/realm-android-library-${project.version}-sources.jar",
                '-u',
                "${userName}:${accessKey}",
                "https://api.bintray.com/content/realm/maven/${artifactId}/${project.version}/io/realm/${artifactId}/${project.version}/${artifactId}-${project.version}-sources.jar?publish=0"
    }

    task("bintrayJavadoc${flavor.name.capitalize()}", type: Exec) {
        dependsOn javadocJar
        group = 'Publishing'
        commandLine 'curl',
                '-X',
                'PUT',
                '-T',
                "${buildDir}/libs/realm-android-library-${project.version}-javadoc.jar",
                '-u',
                "${userName}:${accessKey}",
                "https://api.bintray.com/content/realm/maven/${artifactId}/${project.version}/io/realm/${artifactId}/${project.version}/${artifactId}-${project.version}-javadoc.jar?publish=0"
    }

    task("bintrayPom${flavor.name.capitalize()}", type: Exec) {
        dependsOn "publish${flavor.name.capitalize()}PublicationPublicationToMavenLocal"
        group = 'Publishing'
        commandLine 'curl',
                '-X',
                'PUT',
                '-T',
                "${buildDir}/publications/${flavor.name}Publication/pom-default.xml",
                '-u',
                "${userName}:${accessKey}",
                "https://api.bintray.com/content/realm/maven/${artifactId}/${project.version}/io/realm/${artifactId}/${project.version}/${artifactId}-${project.version}.pom?publish=0"
    }

    // OJO

    task("ojoAar${flavor.name.capitalize()}", type: Exec) {
        dependsOn "assemble${flavor.name.capitalize()}"
        group = 'Publishing'
        commandLine 'curl',
                '-X',
                'PUT',
                '-T',
                "${buildDir}/outputs/aar/realm-android-library-${flavor.name}-release.aar",
                '-u',
                "${userName}:${accessKey}",
                "https://oss.jfrog.org/artifactory/oss-snapshot-local/io/realm/realm-android-library${librarySuffix}/${project.version}/realm-android-library${librarySuffix}-${project.version}.aar?publish=0"
    }

    task("ojoSources${flavor.name.capitalize()}", type: Exec) {
        dependsOn sourcesJar
        group = 'Publishing'
        commandLine 'curl',
                '-X',
                'PUT',
                '-T',
                "${buildDir}/libs/realm-android-library-${project.version}-sources.jar",
                '-u',
                "${userName}:${accessKey}",
                "https://oss.jfrog.org/artifactory/oss-snapshot-local/io/realm/realm-android-library${librarySuffix}/${project.version}/realm-android-library${librarySuffix}-${project.version}-sources.jar?publish=0"
    }

    task("ojoJavadoc${flavor.name.capitalize()}", type: Exec) {
        dependsOn javadocJar
        group = 'Publishing'
        commandLine 'curl',
                '-X',
                'PUT',
                '-T',
                "${buildDir}/libs/realm-android-library-${project.version}-javadoc.jar",
                '-u',
                "${userName}:${accessKey}",
                "https://oss.jfrog.org/artifactory/oss-snapshot-local/io/realm/realm-android-library${librarySuffix}/${project.version}/realm-android-library${librarySuffix}-${project.version}-javadoc.jar?publish=0"
    }

    task("ojoPom${flavor.name.capitalize()}", type: Exec) {
        dependsOn "publish${flavor.name.capitalize()}PublicationPublicationToMavenLocal"
        group = 'Publishing'
        commandLine 'curl',
                '-X',
                'PUT',
                '-T',
                "${buildDir}/publications/${flavor.name}Publication/pom-default.xml",
                '-u',
                "${userName}:${accessKey}",
                "https://oss.jfrog.org/artifactory/oss-snapshot-local/io/realm/realm-android-library${librarySuffix}/${project.version}/realm-android-library${librarySuffix}-${project.version}.pom?publish=0"
    }

    task("bintray${flavor.name.capitalize()}") {
        dependsOn "bintrayAar${flavor.name.capitalize()}"
        dependsOn "bintraySources${flavor.name.capitalize()}"
        dependsOn "bintrayJavadoc${flavor.name.capitalize()}"
        dependsOn "bintrayPom${flavor.name.capitalize()}"
        group = 'Publishing'
    }

    task("ojo${flavor.name.capitalize()}") {
        dependsOn "ojoAar${flavor.name.capitalize()}"
        dependsOn "ojoSources${flavor.name.capitalize()}"
        dependsOn "ojoJavadoc${flavor.name.capitalize()}"
        dependsOn "ojoPom${flavor.name.capitalize()}"
        group = 'Publishing'
    }
}

// Cannot override bintrayUpload in Gradle 4.9. Most likely due to a bug in
// the gradle-bintray-plugin. So we use `bintrayUploadAll` instead that can
// then depend on the original bintrayUpload task.
task bintrayUploadAll() {
    group = 'Publishing'
}

project.afterEvaluate {
    android.productFlavors.all { flavor ->
        bintrayUploadAll.dependsOn "bintray${flavor.name.capitalize()}"
    }
}

task ojoUpload() {
    android.productFlavors.all { flavor ->
        dependsOn "ojo${flavor.name.capitalize()}"
    }
    group = 'Publishing'
}

static def getValueFromPropertiesFile(File propFile, String key) {
    if (!propFile.isFile() || !propFile.canRead()) {
        return null
    }
    def prop = new Properties()
    def reader = propFile.newReader()
    try {
        prop.load(reader)
    } finally {
        reader.close()
    }
    return prop.get(key)
}
