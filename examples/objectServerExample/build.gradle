buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

android {
    compileSdkVersion rootProject.sdkVersion
    buildToolsVersion rootProject.buildTools

    defaultConfig {
        applicationId 'io.realm.examples.objectserver'
        targetSdkVersion rootProject.sdkVersion
        minSdkVersion rootProject.minSdkVersion
        versionCode 1
        versionName "1.0"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        // Go to https://cloud.realm.io and copy the URL to your instance. Insert it below.
        // It will look something like "https://test.us1.cloud.realm.io"
        //
        // If you're running a self-hosted version, use the hostname/IP address of the Realm Object
        // Server, e.g "http://127.0.0.1:9080".
        def rosUrl = "<INSERT_REALM_OBJECT_SERVER_URL>"
        def realmAuthUrl = "\"${rosUrl}/auth\""
        def realmUrl = "\"${rosUrl.replace("http", "realm")}/default\""

        debug {
            buildConfigField "String", "REALM_AUTH_URL", "${realmAuthUrl}"
            buildConfigField "String", "REALM_URL", "${realmUrl}"
        }
        release {
            buildConfigField "String", "REALM_AUTH_URL", "${realmAuthUrl}"
            buildConfigField "String", "REALM_URL", "${realmUrl}"
            minifyEnabled true
            signingConfig signingConfigs.debug
        }
    }
}

realm {
    syncEnabled = true
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
