apply plugin: 'com.android.application'
apply plugin: 'realm-android'

// Credit: http://jeremie-martinez.com/2015/05/05/inject-host-gradle/
def getIP() {
    InetAddress result = null
    Enumeration<NetworkInterface> interfaces = NetworkInterface.getNetworkInterfaces()
    while (interfaces.hasMoreElements()) {
        Enumeration<InetAddress> addresses = interfaces.nextElement().getInetAddresses()
        while (addresses.hasMoreElements()) {
            InetAddress address = addresses.nextElement()
            if (!address.isLoopbackAddress()) {
                if (address.isSiteLocalAddress()) {
                    return address.getHostAddress()
                } else if (result == null) {
                    result = address
                }
            }
        }
    }
    return (result != null ? result : InetAddress.getLocalHost()).getHostAddress()
}

android {
    compileSdkVersion rootProject.sdkVersion
    buildToolsVersion rootProject.buildTools

    defaultConfig {
        applicationId 'io.realm.examples.objectserver'
        targetSdkVersion rootProject.sdkVersion
        minSdkVersion rootProject.minSdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        // This will automatically try to detect the IP address of the machine
        // building the example. It is assumed that this machine is also running
        // the Object Server. If not, replace 'host' with the IP of the machine
        // hosting the server. In some cases the wrong IP address will also
        // be detected. In that case also insert the IP address manually.
        def host = getIP()
        debug {
            buildConfigField "String", "OBJECT_SERVER_IP", "\"${host}\""
            minifyEnabled true
        }
        release {
            buildConfigField "String", "OBJECT_SERVER_IP", "\"${host}\""
            minifyEnabled true
            signingConfig signingConfigs.debug
        }
    }
}

realm {
    syncEnabled = true
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.3.0'
    implementation 'com.jakewharton:butterknife:8.8.1'//TODO:Can be refactored with Native Android Data Binding
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'//TODO:Can be refactored with Native Android Data Binding
}
