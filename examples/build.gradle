project.ext.sdkVersion = 25
project.ext.buildTools = '25.0.2'

// Don't cache SNAPSHOT (changing) dependencies.
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

allprojects {
    def currentVersion = file("${rootDir}/../version.txt").text.trim()

    def props = new Properties()
    props.load(new FileInputStream("${rootDir}/../realm.properties"))
    props.each { key, val ->
        project.ext.set(key, val)
    }

    buildscript {
        repositories {
            mavenLocal()
            jcenter()
            maven { url 'https://jitpack.io' }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.3.1'
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
            classpath 'com.github.JakeWharton:sdk-manager-plugin:0ce4cdf08009d79223850a59959d9d6e774d0f77'
            classpath 'com.novoda:gradle-android-command-plugin:1.5.0'
            classpath "io.realm:realm-gradle-plugin:${currentVersion}"
        }
    }

    group = 'io.realm'
    version = currentVersion

    repositories {
        mavenLocal()
        jcenter()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = project.gradleVersion
}
