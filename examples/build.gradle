project.ext.sdkVersion = 23
project.ext.buildTools = '23.0.0'

def props = new Properties()
props.load(new FileInputStream("${rootDir}/../realm.properties"))
props.each { key, val ->
    project.set(key, val)
}

// Don't cache SNAPSHOT (changing) dependencies.
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

allprojects {
    def currentVersion = file("${rootDir}/../version.txt").text.trim()

    buildscript {
        repositories {
            mavenLocal()
            jcenter()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:1.2.3'
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
            classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
            classpath 'com.novoda:gradle-android-command-plugin:1.3.0'
            classpath "io.realm:gradle-plugin:${currentVersion}"
        }
    }

    group = 'io.realm'
    version = currentVersion

    repositories {
        mavenLocal()
        jcenter()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = project.gradleVersion
}