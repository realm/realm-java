buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def currentVersion = file("${projectDir}/version.txt").text.trim();

version = currentVersion

def commonPom = {
    version = currentVersion
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    issueManagement {
        system 'github'
        url 'https://github.com/realm/realm-java/issues'
    }
    scm {
        url 'scm:https://github.com/realm/realm-java'
        connection 'scm:git@github.com:realm/realm-java.git'
        developerConnection 'scm:git@github.com:realm/realm-java.git'
    }
}

publishing {
    publications {
        aarLibrary(MavenPublication) {
            groupId 'io.realm'
            artifactId = 'realm-android-library'
            artifact file("${rootDir}/realm/realm-library/build/outputs/aar/realm-android-library-release.aar")
            artifact (file("${rootDir}/realm/realm-library/build/libs/realm-android-library-${currentVersion}-sources.jar")) {
                classifier = 'sources'
            }
            artifact (file("${rootDir}/realm/realm-library/build/libs/realm-android-library-${currentVersion}-javadoc.jar")) {
                classifier = 'javadoc'
            }
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'realm-android-library')
                root.appendNode('description', 'Realm is a mobile database: a replacement for SQLite & ORMs.')
                root.appendNode('url', 'http://realm.io')
                root.children().last() + commonPom

                def repositoriesNode = root.appendNode('repositories')
                def repositoryNode = repositoriesNode.appendNode('repository')
                repositoryNode.appendNode('id', 'jitpack.io')
                repositoryNode.appendNode('url', 'https://jitpack.io')

                def dependenciesNode = root.appendNode('dependencies')
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'com.github.KeepSafe')
                dependencyNode.appendNode('artifactId', 'ReLinker')
                dependencyNode.appendNode('version', '1.1')
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : 'noUser'
    key = project.hasProperty('bintrayKey') ? bintrayKey : 'noKey'

    dryRun = false
    publish = false

    publications = ['aarLibrary']
    pkg {
        repo = 'maven'
        name = 'realm-android-library'
        desc = 'Realm for Android'
        websiteUrl = 'http://realm.io'
        issueTrackerUrl = 'https://github.com/realm/realm-java/issues'
        vcsUrl = 'https://github.com/realm/realm-java.git'
        licenses = ['Apache-2.0']
        labels = ['android', 'realm']
        publicDownloadNumbers = false
    }
}
