apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "io.realm.tests.sync"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", "NODE_SERVER_LOCATION", "\"$projectDir\""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn reverseNodeServerPort, reverseSyncServerPort, startServer
    }
}

task reverseNodeServerPort(type: Exec) {
    def adb = android.getAdbExe()?.toString() ?: 'false'
    commandLine adb, 'reverse', 'tcp:8888', 'tcp:8888'
    ignoreExitValue true
    doLast {
        if (execResult.getExitValue() != 0) {
            logger.error(
                    '===========================================================================\n' +
                            'WARNING: Failed to automatically reverse port 8888.\n' +
                            'Please reverse this port from localhost to the device or emulator being used to run the application.\n' +
                            'You may need to add the appropriate flags to the command that failed:\n' +
                            '    adb -s DEVICE reverse tcp:8082 tcp:8082\n' +
                            '===========================================================================\n'
            )
        }
    }
}

task startServer << {
    def processBuilder = new ProcessBuilder(['node','server.js'])
    processBuilder.directory(new File("$projectDir/test_server"))
    processBuilder.start()
}


task reverseSyncServerPort(type: Exec) {
    def adb = android.getAdbExe()?.toString() ?: 'false'
    commandLine adb, 'reverse', 'tcp:7800', 'tcp:7800'
    ignoreExitValue true
    doLast {
        if (execResult.getExitValue() != 0) {
            logger.error(
                    '===========================================================================\n' +
                            'WARNING: Failed to automatically reverse port 7800.\n' +
                            'Please reverse this port from localhost to the device or emulator being used to run the application.\n' +
                            'You may need to add the appropriate flags to the command that failed:\n' +
                            '    adb -s DEVICE reverse tcp:7800 tcp:7800\n' +
                            '===========================================================================\n'
            )
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
}
