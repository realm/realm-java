buildscript {
    def properties = new Properties()
    properties.load(new FileInputStream("${rootDir}/../dependencies.list"))
    ext.kotlin_version = properties.get('KOTLIN')
    repositories {
        mavenLocal()
        google()
        jcenter()
    }
    dependencies {
        classpath "androidx.benchmark:benchmark-gradle-plugin:1.0.0-rc01"
        classpath "com.android.tools.build:gradle:${properties.get("GRADLE_BUILD_TOOLS")}"
        classpath "io.realm:realm-gradle-plugin:${file("${rootDir}/../version.txt").text.trim()}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${properties.get("KOTLIN")}"
    }
}

allprojects {
    def props = new Properties()
    props.load(new FileInputStream("${rootDir}/../dependencies.list"))
    props.each { key, val ->
        project.ext.set(key, val)
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'
apply plugin: 'androidx.benchmark'

android {
    compileSdkVersion 28
    buildToolsVersion "${project.ext.get("ANDROID_BUILD_TOOLS")}"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.benchmark.junit4.AndroidBenchmarkRunner"
        testInstrumentationRunnerArgument 'androidx.benchmark.suppressErrors', 'EMULATOR,UNLOCKED'
    }

    buildTypes {
        debug {
            minifyEnabled = false
            // Running with DEBUG = true will disable the JIT
            debuggable = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
}

dependencies {
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "androidx.benchmark:benchmark-junit4:1.0.0-rc01"
    androidTestImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
