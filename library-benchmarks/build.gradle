buildscript {
    repositories {
        mavenLocal()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-rc2'
        classpath "io.realm:realm-gradle-plugin:${file("${rootDir}/../version.txt").text.trim()}"
    }
}

allprojects {
    def props = new Properties()
    props.load(new FileInputStream("${rootDir}/../realm.properties"))
    props.each { key, val ->
        project.ext.set(key, val)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = project.gradleVersion
}

apply plugin: 'com.android.library'
apply plugin: 'realm-android'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22 // Below 23 to avoid new permission system introduced in M
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled = false
            // Running with DEBUG = true will disable the JIT
            debuggable = false
        }
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
}

dependencies {
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'dk.ilios:spanner:0.6.0'
    androidTestImplementation 'com.opencsv:opencsv:3.4'
    androidTestImplementation 'junit:junit:4.12'
}
